&Пластилин Перем НастройкиПроекта;
&Пластилин Перем ЧтениеИсходников;
&Пластилин Перем РаботаСГит;

&Пластилин(Значение = "ЭлементГлавноеМеню", Тип = "Массив")
Перем ЭлементыГлавноеМеню;

&Контроллер("/")
Процедура ПриСозданииОбъекта()
КонецПроцедуры

&ТочкаМаршрута("")
&Отображение("./src/Классы/interface/view/index.html")
Процедура Главная(Ответ) Экспорт
	Ответ.Модель = Новый Структура("ТекущаяВетка", РаботаСГит.ПолучитьТекущуюВетку());	
КонецПроцедуры

&ТочкаМаршрута("leftMenu")
&Отображение("./src/Классы/interface/view/leftMenu.html")
Процедура ЛевоеМеню(Ответ) Экспорт
	Ответ.Модель = Новый Структура("ЭлементыМеню", ЭлементыГлавноеМеню);
КонецПроцедуры

&ТочкаМаршрута("topMenu")
&Отображение("./src/Классы/interface/view/topMenu.html")
Процедура МенюШапки(Ответ) Экспорт
	
КонецПроцедуры

&ТочкаМаршрута("cfInfo")
&Отображение("./src/Классы/interface/view/cfInfo.html")
Процедура ИнформацияОКонфигурации(Ответ) Экспорт
	Модель = Новый Структура();
	Модель.Вставить("Описание", ЧтениеИсходников.ОписаниеКонфигурации(НастройкиПроекта.КаталогКонфигурации()));
	Модель.Вставить("Цель", "-conf");
	Модель.Вставить("ИспользоватьРедакторМонако", НастройкиПроекта.ИспользоватьРедакторМонако());
	Модель.Вставить("КоличествоИзменений", РаботаСГит.ПолучитьКоличествоИзменений());
	Ответ.Модель = Модель;
КонецПроцедуры

&ТочкаМаршрута("extInfo/{ИмяРасширения}")
&Отображение("./src/Классы/interface/view/cfInfo.html")
Процедура ИнформацияОРасширении(Ответ, ИмяРасширения) Экспорт

	Для каждого Расширение Из НастройкиПроекта.Расширения() Цикл

		Если НЕ Расширение["Имя"] = ИмяРасширения Тогда
			Продолжить;
		КонецЕсли;

		ДанныеРасширения = ЧтениеИсходников.ОписаниеКонфигурации(Расширение["Каталог"]);

		Ответ.Модель = Новый Структура("Описание, Цель", ДанныеРасширения, ИмяРасширения);

		Прервать;
		
	КонецЦикла;

КонецПроцедуры

&ТочкаМаршрута("extList")
&Отображение("./src/Классы/interface/view/extList.html")
Процедура СписокРасширений(Ответ) Экспорт

	Ответ.Модель = Новый Структура("МассивРасширений", Новый Массив);

	Для каждого Расширение Из НастройкиПроекта.Расширения() Цикл
		ДанныеРасширения = ЧтениеИсходников.ОписаниеКонфигурации(Расширение["Каталог"]);
		Если НЕ ЗначениеЗаполнено(ДанныеРасширения.Имя) Тогда
			ДанныеРасширения.Имя = Расширение["Имя"];
		КонецЕсли;
		Ответ.Модель.МассивРасширений.Добавить(ДанныеРасширения);
	КонецЦикла;

КонецПроцедуры