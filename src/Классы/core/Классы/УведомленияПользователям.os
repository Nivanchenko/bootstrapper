&Пластилин Перем БрокерСообщенийСобытийСервера;
&пластилин Перем ФабрикаОтветов;

Перем КаналКонсоли;
Перем КаналУведомлений;

&Желудь
Процедура ПриСозданииОбъекта(&Пластилин ТопикиСерверныхСобытий)

	КаналКонсоли = "/console";
	КаналУведомлений = "/notifications";

	ТопикиСерверныхСобытий.Добавить(КаналКонсоли);
	ТопикиСерверныхСобытий.Добавить(КаналУведомлений);

КонецПроцедуры

Процедура СобытиеЛога(ТекстСобытия) Экспорт

	Сообщение = ФабрикаОтветов.СерверноеСобытие();
	Сообщение.ТипСобытия("log")
				.ДобавитьСтроку(ОформитьТекстЛога(ТекстСобытия));
	БрокерСообщенийСобытийСервера.ОтправитьСообщениеВсем(КаналКонсоли, Сообщение);
	
КонецПроцедуры

Процедура СобытиеУведомление(ТекстСобытия, Заголовок = "") Экспорт

	Структура = Новый Структура("title, content", Заголовок, ТекстСобытия);

	ЗаписьJSON = Новый ЗаписьJSON();
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, Структура);
	ТекстСообщения = ЗаписьJSON.Закрыть();

	Сообщение = ФабрикаОтветов.СерверноеСобытие();
	Сообщение.ТипСобытия("notification")
				.ДобавитьСтроку(ТекстСообщения);
	БрокерСообщенийСобытийСервера.ОтправитьСообщениеВсем(КаналУведомлений, Сообщение);
	
КонецПроцедуры

Функция ОформитьТекстЛога(Знач Текст)

	Текст = СтрЗаменить(Текст, Символы.ПС, "<br>");
	Текст = СтрЗаменить(Текст, Символы.ВК, "<br>");

	Шаблон = ("<span style=""text-wrap: wrap;"" >%1</span><br>");
	Возврат СтрШаблон(Шаблон, Текст);

КонецФункции
