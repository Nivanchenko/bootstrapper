#Использовать fs

&Пластилин 
Перем НастройкиПроекта;

&Табакерка
&Пластилин
Перем КомандаВраннер;

Перем Настройки;

&Желудь
Процедура ПриСозданииОбъекта(&Пластилин НастройкиРабочейОбласти)
	Настройки = НастройкиРабочейОбласти;
КонецПроцедуры

Функция РазобратьКонфигурацию() Экспорт
	КаталогКонфигурации = НастройкиПроекта.КаталогКонфигурации();
	Если НЕ ЗначениеЗаполнено(КаталогКонфигурации) Тогда
		ВызватьИсключение "Не указан каталог конфигурации";
	КонецЕсли;

	// Для ИБКМД нужно предварительно очистить каталог выгрузки
	Если Настройки.ИБКМД = Истина Тогда
		ФС.ОбеспечитьПустойКаталог(КаталогКонфигурации);
	КонецЕсли;

	Команда = КомандаВраннер.Достать();

	Команда.ДобавитьПараметр("decompile")
			.ДобавитьПараметр("--current")
			.ДобавитьПараметр("--out=" + КаталогКонфигурации);

	Возврат Команда.Исполнить();
КонецФункции

Функция СобратьКонфигурацию() Экспорт
	КаталогКонфигурации = НастройкиПроекта.КаталогКонфигурации();
	Если НЕ ЗначениеЗаполнено(КаталогКонфигурации) Тогда
		ВызватьИсключение "Не указан каталог конфигурации";
	КонецЕсли;

	Команда = КомандаВраннер.Достать();

	Команда.ДобавитьПараметр("compile")
			.ДобавитьПараметр("--current")
			.ДобавитьПараметр("--src=" + КаталогКонфигурации);

	Возврат Команда.Исполнить();
КонецФункции

Функция РазобратьРасширение(ИмяРасширения) Экспорт

	Каталог = НастройкиПроекта.КаталогРасширенияПоИмени(ИмяРасширения);

	Если НЕ ЗначениеЗаполнено(Каталог) Тогда
		ВызватьИсключение "Не указан каталог расширения " + ИмяРасширения;
	КонецЕсли;

	// Для ИБКМД нужно предварительно очистить каталог выгрузки
	Если Настройки.ИБКМД = Истина Тогда
		ФС.ОбеспечитьПустойКаталог(Каталог);
	КонецЕсли;

	Команда = КомандаВраннер.Достать();

	Команда.ДобавитьПараметр("decompileext")
			.ДобавитьПараметр(ИмяРасширения)
			.ДобавитьПараметр(Каталог)
			;

	Возврат Команда.Исполнить();

КонецФункции

Функция СобратьРасширение(ИмяРасширения) Экспорт

	Каталог = НастройкиПроекта.КаталогРасширенияПоИмени(ИмяРасширения);

	Если НЕ ЗначениеЗаполнено(Каталог) Тогда
		ВызватьИсключение "Не указан каталог расширения " + ИмяРасширения;
	КонецЕсли;

	Команда = КомандаВраннер.Достать();

	Команда.ДобавитьПараметр("compileext")
			.ДобавитьПараметр(Каталог)
			.ДобавитьПараметр(ИмяРасширения);

	Возврат Команда.Исполнить();

КонецФункции

Функция СобратьОбработку(КаталогИсходников, КаталогБинарников) Экспорт

	Команда = КомандаВраннер.Достать();

	Команда.ДобавитьПараметр("compileepf")
			.ДобавитьПараметр(КаталогИсходников)
			.ДобавитьПараметр(КаталогБинарников)
			.ВыключитьИБКМД();

	Возврат Команда.Исполнить();

КонецФункции

Функция РазобратьОбработку(КаталогБинарников, КаталогИсходников) Экспорт

	Команда = КомандаВраннер.Достать();

	Команда.ДобавитьПараметр("decompileepf")
			.ДобавитьПараметр(КаталогБинарников)
			.ДобавитьПараметр(КаталогИсходников)
			.ВыключитьИБКМД();

	Возврат Команда.Исполнить();

КонецФункции

